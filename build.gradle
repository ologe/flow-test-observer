plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
}

group 'com.github.ologe'
project.ext.artifactId = 'flow-test-observer'
version '1.5.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    def coroutines = '1.4.1'
    def junit = '4.13.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines"
    implementation "junit:junit:$junit"

    testImplementation "junit:junit:$junit"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = true
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.FlowPreview"
        ]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = true
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.FlowPreview"
        ]
    }
}

task sourceJar(type: Jar){
    from sourceSets.main.kotlin
}

publishing {
    publications {
        release(MavenPublication) {

            groupId = project.group
            artifactId = project.artifactId
            version = project.version

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}